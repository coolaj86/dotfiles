#!/usr/bin/env zsh

# XDG settings
export XDG_CACHE_HOME=${HOME}/.cache
export XDG_DATA_HOME=${HOME}/.local/share
export XDG_BIN_HOME=${HOME}/.local/bin
export XDG_CONFIG_HOME=${HOME}/.config

# Root dir of dotfiles repo
export ZSH=${XDG_CONFIG_HOME}/dotfiles

# all of our zsh files
typeset -U config_files
config_files=( $ZSH/**/*.zsh )

# load the path files
for file in ${(M)config_files:#*/path.zsh}; do
  source "$file"
done

# setup antibody
antibody bundle < "${ANTIBODY_BUNDLE_FILE}"
#source "${ANTIBODY_HOME}/zsh-users-zsh-history-substring-search/zsh-history-substring-search.zsh"

# load everything but the path and completion files
for file in ${${config_files:#*/path.zsh}:#*/completion.zsh}; do
  source "$file"
done

# initialize autocomplete here, otherwise functions won't be loaded
autoload -U compinit
compinit -i -d ${ZSH_COMPDUMP}

# load every completion after autocomplete loads
for file in ${(M)config_files:#*/completion.zsh}; do
  source "$file"
done

unset config_files

# use .localrc for SUPER SECRET CRAP that you don't
# want in your public, versioned repo.
#[[ -a ~/.localrc ]] && source ~/.localrc

# TODO: figure out why specifying this in the bundle file causes everything to explode
antibody bundle mkwmms/zsh-fasd

REPO_URI='https://github.com/mkwmms/spf13-vim.git'
